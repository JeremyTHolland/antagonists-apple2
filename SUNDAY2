100 REM  APPLE - THE ANTAGONISTS
110 GOSUB 2000: REM INIT
120 CALL ALADIN
130 BAS = PEEK (SA + 30)
140 ON BAS GOSUB 700,1030,200,940,990,1030,1090,1160,1210,1270
150 POKE 111,P1: POKE 112,P2
160 GOTO 120
170 END
200 REM *** DISPLAY TEXT ***
210 S1 = PEEK (27500) + PEEK (27501) * 256
220 CC = 0
230 TA$ = TX$(S1 + VS)
240 IF LEN (TA$) = 0 THEN PRINT : RETURN
250 CH$ = LEFT$ (TA$,2):TA$ = MID$ (TA$,3)
260 WD$ = TX$(128 * ( ASC ( LEFT$ (CH$,1)) - 48) + ASC ( RIGHT$ (CH$,1)))
270 IF LEFT$ (WD$,1) = "#" THEN 350
280 CC = CC + LEN (WD$) + 1
290 IF CC < CW THEN 320
300 PRINT
310 CC = LEN (WD$) + 1
320 PRINT WD$;" ";
330 GOTO 240
340 RETURN
350 JP = VAL ( MID$ (WD$,2))
360 IF JP = 0 THEN PRINT : GOTO 240
370 IF JP = 8 THEN TA$ = TX$( PEEK (SA + 31 + JP) + VS): GOTO 240
380 IF JP > 13 THEN WD$ = STR$ ( PEEK (SA + 31 + JP)): GOTO 280
390 TA$ = TX$( PEEK (SA + 31 + JP) + VS + TS) + TA$
400 GOTO 240
500 REM ******** READ DATA FROM DISK **********************
510 PRINT "READING TEXT DATA (60 SECONDS)"
520 PRINT D$;"OPEN DATAFILE"
530 PRINT D$;"READ DATAFILE"
540 INPUT NC,US,TS,NT
550 DIM CM%(NC,3)
560 FOR I = 1 TO NC
570   INPUT CM%(I,1),CM%(I,2),CM%(I,3)
580 NEXT
590 NR = VS + TS + NT
600 DIM TX$(NR)
610 FOR I = 1 TO NR
620   INPUT TX$(I)
630 NEXT
640 PRINT R$;D$;"CLOSE DATAFILE"
650 RETURN
700 REM ******************* PARSER **********************
710 POKE - 16368,0:PO = 0:LW = 0
720 INPUT "";C$
730 IF C$ = "" THEN RETURN
740 LS = LEN (C$)
750 PO = PO + LW + 1
760 LW = 0
770 IF PO > LS THEN RETURN
780 FOR I = PO TO LS
790   IF MID$ (C$,I,1) = " " THEN I = LS:LW = LW - 1
800   LW = LW + 1
810 NEXT
820 IF LW = 0 THEN 750
830 C1$ = MID$ (C$,PO,LW)
840 CC = 0
850 FOR J = 1 TO NC
860   IF C1$ = LEFT$ (TX$(CM%(J,1)),LW) THEN CC = J:J = NC
870 NEXT
880 IF CC = 0 THEN 750
890 ON CM%(CC,3) GOTO 900,910
900 POKE SA + 1,CM%(CC,2): GOTO 750
910 IF PEEK (SA + 2) = 0 THEN POKE SA + 2,CM%(CC,2): GOTO 750
920 POKE SA + 3,CM%(CC,2)
930 GOTO 750
940 REM *** RANDOM GENERATOR ***
950 HC = PEEK (SA + 28)
960 HC = INT ( RND (1) * HC) + 1
970 POKE SA + 22,HC
980 RETURN
990 REM *** RETURN FROM ALADIN SUBROUTINE ***
1000 AP = SP
1010 GOSUB 2220
1020 RETURN
1030 REM *** ALADIN SUBROUTINE ***
1040 SP = PEEK (250) * 256 + PEEK (249)
1050 INDEX = 7: GOSUB 2180
1060 AP = START + 4700
1070 GOSUB 2220
1080 RETURN
1090 REM ******* LOAD *******
1100 PRINT "PRESS <RETURN> TO CANCEL OR (?) FOR CATALOG"
1110 INPUT "FILENAME: ";NA$
1120 IF NA$ = "" THEN RETURN
1130 IF NA$ = "?" THEN PRINT D$;"CATALOG": GOTO 1100
1140 PRINT D$;"BLOAD " + NA$
1150 RETURN
1160 REM ******* SAVE *******
1170 INPUT "FILENAME (PRESS <RETURN> TO CANCEL): ";NA$
1180 IF NA$ = "" THEN RETURN
1190 PRINT D$;"BSAVE " + NA$;",A27500,L2500"
1200 RETURN
1210 REM  ****** RESTART *******
1220 PRINT "DO YOU WANT TO RESTART OR"
1230 INPUT "TO CONTINUE (R/C) ";CO$
1240 IF CO$ < > "R" THEN RETURN
1250 SP = 0
1260 GOTO 2120
1270 REM *** LOOP ***
1280 FOR I = 1 TO 10
1290   FOR J = 1 TO 1000: NEXT J
1300   PRINT CHR$ (7);: REM CONTROL G
1310 NEXT I
1320 RETURN
2000 REM ******************** INIT **********************
2010 HIMEM: 27500
2020 ADR = 27502
2030 D$ = CHR$ (4):R$ = CHR$ (13)
2040 CW = 39: HOME
2050 PRINT "IS THIS AN APPLE 2E WITH 80-COLUMN"
2060 INPUT "DISPLAY (Y/N)? ";CO$
2070 IF CO$ = "Y" THEN PRINT D$;"PR#3":CW = 79
2080 GOSUB 500: REM READ DATA FROM DISK
2090 PRINT "CLEARING MEMORY (90 SECONDS)"
2100 P1 = FRE (0):P1 = PEEK (111):P2 = PEEK (112)
2110 PRINT "LOADING DATA (15 SECONDS)"
2120 PRINT D$;"BLOAD ALADATA"
2130 INDEX = 1: GOSUB 2180:SA = START
2140 INDEX = 7: GOSUB 2180:AP = START
2150 INDEX = 8: GOSUB 2180:ALADIN = START
2160 GOSUB 2220: HOME
2170 RETURN
2180 REM  GET START ADDRESS
2190 INDEX = (INDEX - 1) * 2 + ADR
2200 START = PEEK (INDEX + 1) * 256 + PEEK (INDEX)
2210 RETURN
2220 REM  POKE ALADIN POINTER (AP)
2230 HB = INT (AP / 256):LB = AP - HB * 256
2240 POKE 250,HB: POKE 249,LB
2250 RETURN